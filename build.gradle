plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.10'
    id 'maven-publish'
    id 'signing'
}

ext {
    projectDescription = "A wrapper for mdbg.net's CC-CEDICT"
    projectVersion = '0.1.4'
    projectUrl = 'https://github.com/patarapolw/cedict'
    projectGit = 'https://github.com/patarapolw/cedict.git'
    projectGitSsh = 'git@github.com:patarapolw/cedict.git'
}

archivesBaseName = 'cedict'
group = 'io.github.patarapolw'
version = project.projectVersion
description = project.projectDescription

repositories {
    jcenter()
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    implementation 'org.xerial:sqlite-jdbc:3.25.2'

    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.0.0'
}

task sourceJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourceJar
    archives javadocJar
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = group
            artifactId = archivesBaseName
            version = projectVersion

            from components.java

            artifact(sourceJar) {
                classifier = 'sources'
            }
            artifact(javadocJar) {
                classifier = 'javadoc'
            }

            pom {
                name = archivesBaseName
                description = projectDescription
                url = projectUrl
                licenses {
                    license {
                        name = 'The MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'patarapolw'
                        name = 'Pacharapol Withayasakpunt'
                        email = 'patarapolw@gmail.com'
                    }
                }
                scm {
                    connection = "scm:git:$projectGit"
                    developerConnection = "scm:git:$projectGitSsh"
                    url = project.projectUrl
                }
            }
        }
    }

    repositories {
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username sonatypeUsername
                password sonatypePassword
            }
        }
    }
}

signing {
    sign configurations.archives
}

