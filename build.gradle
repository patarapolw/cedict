plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.10'
    id 'maven'
    id 'signing'
}

ext {
    projectDescription = "A wrapper for mdbg.net's CC-CEDICT"
    projectVersion = '0.1.4'
    projectUrl = 'https://github.com/patarapolw/cedict'
    projectGit = 'https://github.com/patarapolw/cedict.git'
    projectGitSsh = 'git@github.com:patarapolw/cedict.git'
}

archivesBaseName = 'cedict'
group = 'io.github.patarapolw'
version = project.projectVersion
description = project.projectDescription

repositories {
    jcenter()
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    implementation 'org.xerial:sqlite-jdbc:3.25.2'

    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.0.0'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    useGpgCmd()
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                name archivesBaseName
                packaging 'jar'
                description projectDescription
                url "https://github.com/patarapolw/$archivesBaseName"

                scm {
                    connection = "scm:git:git@github.com:patarapolw/${archivesBaseName}.git"
                    url = "https://github.com/patarapolw/${archivesBaseName}"
                }

                licenses {
                    license {
                        name = 'The MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id = 'patarapolw'
                        name = 'Pacharapol Withayasakpunt'
                        email = 'patarapolw@gmail.com'
                    }
                }
            }
        }
    }
}